

PUT_DEBUG_ATTR_SIZE EQU 1

	ORG	00400000h

IMAGE_BASE equ	00400000h

BASE:

#INCLUDE <HEADER-C.ASM>



;
;	upper case!=lower case
;

	;;ORG	0

ID_ABS_MAXLEN	EQU	0FH

ID_ABS		EQU	08H
ID_DEFINED	EQU	10H
ID_PUBLIC	EQU	40H
ID_EXTRN	EQU	80H

ID_OFFSET	EQU	84H

ID_EOF		EQU	0FFH

;EXTRN_LINK	EQU	080000H
;EXTRN_LINK_END	EQU	0C0000H
EXTRN_LINK_MEMORY_SIZE EQU	20000H

LINESIZE EQU	1024

R.BUF	EQU	4000H;1000H;8000H;1000H	;buffer for read
W.BUF	EQU	4000H;800H;8000H;800H	;buffer for write

;W.LBL	EQU	30000h;100h;6000H;8000H	;label
W.LBL	EQU	USRDMA+R.BUF+W.BUF+R.BUF

;EOM	EQU	7FF00H;60000h;0FF00H	;0E000H	;end of memory
EOM	EQU	W.LBL+20000h-100h

ENTRY_MAIN:
	CALL	INIT_MEMORY

	;CLI
	;mov	esp,0D0000H	;80000h
	;STI
	CLD

	;CALL	SETINT
	CALL	GETCLINE
	CALL	EXTCLINE

	MOV	EAX,DS
	MOV	ES,EAX

	MOV	ESI,CLINE+1
	MOV	EDI,FILE1
	CALL	STRCPY

	XOR	AX,AX
	MOV	[FLG.1],AL
	MOV	[FLG.2],AL
	MOV	[FLG.3],AL

	MOV	CL,0
	MOV	[FLG.1],CL

	MOV	ESI,FILE1
	MOV	EDI,FILE2
	CALL	STRCPY

	MOV	ESI,FILE1
	MOV	AL,'.'
	CALL	STRCHR
	JE	I3

	MOV	ESI,FILE1
	MOV	EDI,STR_ASM
	CALL	STRCAT
	JMP	I3.1

I3:
	MOV	BYTE [ESI],0
	PUSH	ESI

	MOV	ESI,FILE1
	MOV	EDI,FILE2
	CALL	STRCPY

	POP	ESI
	MOV	BYTE [ESI],'.'

I3.1:
	MOV	EDX,MES1
	CALL	DOS09

	XOR	AL,AL
	MOV	[F.TYPE],AL

	;MOV	SI,0081H
	MOV	ESI,CLINE+1

SW:	MOV	AL,[ESI]
	INC	ESI
	OR	AL,AL
	JE	SW90

	CMP	AL,'/'
	JNE	SW
	MOV	AL,[ESI]
		CALL	UPPER
	INC	ESI
	CMP	AL,'3'
	JNE	SW.10
	MOV	AL,0FFH
	MOV	[FLG.386],AL
	MOV	[FLG.DWORD],AL
	JMP	SW

SW.10:	CMP	AL,'L'
	JNE	SW.20
	MOV	AL,0FFH
	MOV	[FLG.L],AL
	JMP	SW

SW.20:	CMP	AL,'P'
	JNE	SW.30
	MOV	AL,0FFH
	MOV	[FLG.P],AL
	JMP	SW

SW.30:
	CMP	AL,'E'
	JNE	SW.40
	MOV	AL,0FFH
	MOV	[FLG.E],AL
	JMP	SW
SW.40:
	CMP	AL,'X'
	JNE	SW.50
	MOV	BYTE [FLG.LINK],0FFH
	CALL	GET_FNAMES

	JMP	SW90
	JMP	SW

SW.50:
	CMP	AL,'S'
	JNE	SW.60
	MOV	BYTE [FLG.S],0FFH
	JMP	SW

SW.60:
	CMP	AL,'C'
	JNE	SW.70
	MOV	AL,[ESI]
	CALL	UPPER
	CMP	AL,'L'
	JNE	SW
	INC	ESI
	MOV	BYTE [FLG.CL],0FFH	;case lower
	JMP	SW


SW.70:
	CMP	AL,'J'
	JNE	SW.80
	MOV	BYTE [FLG.J],0FFH	;jump shorten
	JMP	SW


SW.80:
	CMP	AL,'O'		;output filename
	JNE	SW.81
	MOV	EDI,FILE2
	CALL	STRCPY
	JMP	SW
SW.81:
	CMP	AL,'A'
	JNE	SW.82
	MOV	BYTE [FLG.A],0FFH	;align module head
	JMP	SW


SW.82:
	CMP	AL,'Q'
	JNE	SW.83
	MOV	BYTE [FLG.Q],0FFH	;reduce format
	JMP	SW

SW.83:
	CMP	AL,'W'
	JNE	SW.84
	MOV	AL,0FFH
	MOV	[FLG.W],AL

	MOV	AL,[ESI]
	CMP	AL,'W'
	JNE	SW

	INC	ESI
	MOV	AL,0FFH
	MOV	[FLG.TEXT_WRITABLE],AL
	JMP	SW


SW.84:
	CMP	AL,'D'
	JNE	SW.85
	MOV	AL,0FFH
	MOV	[FLG.D],AL
	JMP	SW
SW.85:
	JMP	SW

SW90:
	CMP	BYTE [FLG.E],0
	JNE	SW90.EXP

	CMP	BYTE [FLG.LINK],0
	JNE	SW90.COM	;SW90.EXP

	MOV	ESI,FILE2
	MOV	EDI,STR_REL	;STR_COM
	CALL	STRCAT
	JMP	SW90.EXP.E
SW90.EXP:
	MOV	ESI,FILE2
	MOV	EDI,STR_EXP
	CALL	STRCAT
	JMP	SW90.EXP.E

SW90.COM:
	MOV	ESI,FILE2
	MOV	EDI,STR_COM
	CALL	STRCAT

SW90.EXP.E:
	CMP	BYTE [FLG.LINK],0FFH
	JE	LINK

	MOV	EDX,FILE1
	MOV	AX,3D00H	;open
	CALL	INT21_open	;INT	21H
	JB	ERR0
	MOV	[HDL],EAX

	MOV	EDX,FILE2
	MOV	CX,0
	MOV	AX,3C00H	;create
	CALL	INT21_create	;INT	21H
	JB	ERR2	;ERR0
	MOV	[HDL2],EAX

	MOV	AL,0FFH
	MOV	[FLAG_FILE2],AL

	MOV	EBX,W.BUF
	MOV	[W.LEFT],EBX
	MOV	EBX,USRDMA+R.BUF
	MOV	[W.PNT],EBX

	MOV	EBX,LBL.PNT.TOP
	MOV	ECX,400H+400H	;200H+200H
INIT50:	XOR	AL,AL
	MOV	[EBX],AL
	INC	EBX
	LOOP	INIT50

MAIN:	MOV	AL,1
	MOV	[PASS],AL
	CALL	ASM

	CALL	PUT.L

		MOV	EAX,[O.ADR1]
		MOV	[PREV_O.ADR1],EAX

MAIN.OPTJ.L1:
	MOV	AL,2
	MOV	[PASS],AL

	CALL	ASM

		CMP	BYTE [FLG.J],0
		JE	MAIN.OPTJ.E

		MOV	EAX,[O.ADR1]
		CMP	EAX,[PREV_O.ADR1]
		MOV	[PREV_O.ADR1],EAX
		JE	MAIN.OPTJ.E

		MOV	EBX,W.BUF
		MOV	[W.LEFT],EBX
		MOV	EBX,USRDMA+R.BUF
		MOV	[W.PNT],EBX

		MOV	CX,0
		MOV	DX,0
		MOV	AX,4200H	;seek
		MOV	EBX,[HDL2]
		CALL	INT21_seek	;INT	21H

		JMP	MAIN.OPTJ.L1

MAIN.OPTJ.E:

MAIN_FLUSH:
		CALL	FLUSH_ABS

		MOV	AL,ID_EOF
		CALL	PUTCHR

	MOV	EBX,W.BUF
	MOV	EDX,[W.LEFT]
	SUB	EBX,EDX
	JE	I10

	MOV	ECX,EBX
	MOV	EDX,USRDMA+R.BUF
	MOV	EBX,[HDL2]
	MOV	AX,4000H	;write
	PUSH	ECX
	CALL	INT21_write	;INT	21H
	POP	ECX
	JB	ERR3

	CMP	EAX,ECX
	JNE	ERR3

;	JB	ERR3
I10:
		MOV	AX,4000H
		MOV	EBX,[HDL2]
		MOV	ECX,0		;truncate
		MOV	EDX,0
		CALL	INT21_truncate	;INT	21H

I12:
	CMP	BYTE [FLG.E],0
	JE	I12.EXP

	CALL	WRITE_EXP_HEADER

I12.EXP:

	CMP	BYTE [FLG.W],0
	JE	I12.WIN

	CALL	WRITE_WIN_HEADER

	MOV	EBX,[HDL2]
	MOV	ECX,0
	MOV	EDX,0
	;MOV	AX,4201H	;seek
	MOV	AX,4202h
	;INT	21H		;>dx:ax
	CALL	INT21_seek

I12.WIN:
	CALL	PUT_DEBUG

	CALL	WRITE_COFF_DEBUG_HEADER

	MOV	EBX,[HDL2]
	MOV	AX,3E00H	;close
	CALL	INT21_close	;INT	21H
	JB	ERR3

	XOR	AL,AL
	MOV	[FLAG_FILE2],AL

	MOV	EDX,MES10
	CALL	DOS09

	MOV	EBX,[O.ADR1]
	DEC	EBX
	CALL	PUTH4

	CALL	CRLF

	MOV	EDX,MES11
	CALL	DOS09

	MOV	BX,[LBL.NO]
	CALL	PUTDC


	CALL	CRLF

	CALL	PUT.L

EXIT:
	MOV	AL,[EXITCODE]
	MOV	AH,4CH
	CALL	INT21_exit	;INT	21H

;
;
;
EXTCLINE:
	MOV	ESI,CLINE+1
EXTCLINE.10:
	MOV	AL,[ESI]
	INC	ESI
	OR	AL,AL
	JE	EXTCLINE.R
	CMP	AL,'@'
	JE	EXTCLINE.15
	JMP	EXTCLINE.10

EXTCLINE.15:
	MOV	EDI,FILECL
	CALL	STRCPY

	MOV	EDX,FILECL
	MOV	AX,3D00H	;open
	MOV	CX,0
	CALL	INT21_open	;INT	21H
	JB	ERR0

	MOV	BX,AX
	MOV	EDX,CLINE+1
	MOV	ECX,400H
	MOV	AX,3F00H
	CALL	INT21_read	;INT	21H
	MOV	BYTE [CLINE+400H],0

	MOV	ESI,CLINE+1
EXTCLINE.20:
	MOV	AL,[ESI]
	INC	ESI
	CMP	AL,20H
	JNB	EXTCLINE.20

	DEC	ESI
	MOV	BYTE [ESI],0

EXTCLINE.R:
	RET




#INCLUDE <WRWIN-R.ASM>
#INCLUDE <WRCOFF-C.ASM>
#INCLUDE <PUTDEB-C.ASM>

;
;
;
PUT.L:
	MOV	AL,[FLG.L]
	OR	AL,AL
	JE	PUT.L.E

	MOV	DX,[LBL.NO]
	OR	DX,DX
	JE	PUT.L.E
	MOV	EBX,W.LBL

PUT.L0:	MOV	CH,[EBX]		;len
	INC	EBX

	;INC	BX		;link pnt skip
	;INC	BX

	ADD	EBX,4

PUT.L1:	MOV	AL,[EBX]		;string
	CALL	PUT.L20
	INC	EBX
	DEC	CH
	JNE	PUT.L1

	MOV	AL,09H
	CALL	PUT.L20
	MOV	AL,[EBX]		;value

	MOV	AL,[EBX+3]
	CALL	PUT.L30
	MOV	AL,[EBX+2]
	CALL	PUT.L30
	MOV	AL,[EBX+1]
	CALL	PUT.L30
	MOV	AL,[EBX]
	CALL	PUT.L30
	ADD	EBX,4

	MOV	AL,20H
	CALL	PUT.L20

	MOV	AL,[EBX]
	CALL	PUT.L30
	INC	EBX		;attr

	MOV	AL,0DH
	CALL	PUT.L20
	MOV	AL,0AH
	CALL	PUT.L20

	DEC	DX
	OR	DX,DX
	JNE	PUT.L0

PUT.L.E:
	RET
;
GET_FNAMES:
	MOV	EDX,FNAMES
GET_FNAMES.LP1:
	MOV	[EDX],ESI
	ADD	EDX,4
GET_FNAMES.LP2:
	MOV	AL,[ESI]
	INC	ESI
	CMP	AL,20H
	JB	GET_FNAMES.E
	JA	GET_FNAMES.LP2

	JMP	GET_FNAMES.LP1
	

GET_FNAMES.E:
	XOR	EAX,EAX
	MOV	[EDX],EAX
	RET

;
;
;
PUT.P:
	CMP	BYTE [PASS],1
	JE	PUT.P.R

	MOV	AL,[FLG.L]
	OR	AL,AL
;	JE	PUT.P.E

	MOV	DX,[LBL.NO]
	OR	DX,DX
	JE	PUT.P.E
	MOV	EBX,W.LBL

PUT.P0:	MOV	CH,[EBX]		;len


	MOVZX	EDI,CH
	ADD	EDI,EBX
	ADD	EDI,1+4+4
	MOV	AL,[EDI]
	TEST	AL,0C0H
	JE	PUT.PS

	TEST	AL,40H
	JE	PUT.P.1

	TEST	AL,10H
	JNE	PUT.P.1

	PUSHAD
	MOV	EDX,EMES_PUBLIC_UNDEF
	MOV	AH,9
	CALL	INT21_putstr	;INT	21H
	POPAD

	PUSHAD
	MOV	ESI,EBX
	CALL	DISP_LBL2
	MOV	AL,0DH
	CALL	DOS02
	MOV	AL,0AH
	CALL	DOS02
	POPAD

PUT.P.1:
	INC	EBX
	ADD	EBX,4		;link

	MOV	AL,CH
	CALL	PUTCHR.P

PUT.P1:	MOV	AL,[EBX]		;string
	CMP	BYTE [FLG.CL],0
	JE	PUT.P1.50

	CALL	LOWER

PUT.P1.50:
	CALL	PUTCHR.P
	INC	EBX
	DEC	CH
	JNE	PUT.P1

	MOV	AL,[EBX]
	CALL	PUTCHR.P
	MOV	AL,[EBX+1]
	CALL	PUTCHR.P
	MOV	AL,[EBX+2]
	CALL	PUTCHR.P
	MOV	AL,[EBX+3]
	CALL	PUTCHR.P
	ADD	EBX,4

	MOV	AL,[EBX]
	CALL	PUTCHR.P
	INC	EBX		;attr

	JMP	PUT.P.LE
PUT.PS:
	MOV	EBX,EDI
	INC	EBX

PUT.P.LE:
	DEC	DX
	OR	DX,DX
	JNE	PUT.P0
PUT.P.E:

	MOV	AL,0
	CALL	PUTCHR.P
PUT.P.R:
	RET

;
PUTCHR.P:
	PUSHAD
	CALL	PUTCHR
	POPAD
	RET






;

LINK:
	MOV	[STACK],ESP

	MOV	AL,1
	MOV	[PASS],AL

	MOV	EBX,W.LBL
	MOV	[LBL.NX],EBX
	XOR	EBX,EBX
	MOV	[LBL.NO],BX
	MOV	[ERR.NO],BX


	MOV	EDX,FILE2
	MOV	CX,0
	MOV	AX,3C00H	;create
	CALL	INT21_create	;INT	21H
	JB	ERR2	;ERR0
	MOV	[HDL2],EAX

	CMP	BYTE [FLG.E],0
	JE	LINK.H.EXP

	MOV	AX,4000H
	MOV	EBX,[HDL2]
	MOV	ECX,200H
	MOV	EDX,USRDMA	;just for seek
	CALL	INT21_write	;INT	21H
LINK.H.EXP:
	CMP	BYTE [FLG.W],0
	JE	LINK.H.WIN

	MOV	AX,4000H
	MOV	EBX,[HDL2]
	MOV	ECX,FILE_ALIGN
	MOV	EDX,HEADER_BUF	;just for seek
	CALL	INT21_write	;INT	21H

LINK.H.WIN:


	MOV	AL,0FFH
	MOV	[FLAG_FILE2],AL

	MOV	EBX,W.BUF
	MOV	[W.LEFT],EBX
	MOV	EBX,USRDMA+R.BUF
	MOV	[W.PNT],EBX


	MOV	EBX,LBL.PNT.TOP
	MOV	ECX,400H+400H	;200H+200H
LINIT50:
	XOR	AL,AL
	MOV	[EBX],AL
	INC	EBX
	LOOP	LINIT50

	MOV	ESI,FNAMES
	MOV	[FNAMES_PNT],ESI

	;;MOV	DWORD [LOC],0
	MOV	DWORD [LOC],00401000H

LLOOP.10:
	;MOV	AH,2
	;MOV	DL,'@'
	;INT	21H
	MOV	AH,1DH
	MOV	AL,'@'
	CALL	INT91_1D	;INT	91H

	CALL	ALIGN_MODULE

	MOV	EAX,[LOC]
	MOV	[TOP_LOC],EAX

	CALL	LINKFILE
	JB	LLOOP.E

	JMP	LLOOP.10

LLOOP.E:
	CALL	FLUSH

	CALL	SORT_EXTRN
	CALL	PATCH_EXTRN

	CALL	CHECK_EXTRN

	MOV	EAX,[LOC]
	MOV	[O.ADR1],EAX

		MOV	[O.BYTE],EAX
	JMP	I12

	JMP	MAIN_FLUSH

	RET


;
;
;
LINKFILE:
	MOV	ESI,[FNAMES_PNT]
	MOV	EDX,[ESI]
	ADD	ESI,4
	MOV	[FNAMES_PNT],ESI

	OR	EDX,EDX
	JE	LINKFILE.E

	MOV	ESI,EDX
	MOV	EDI,FILE1
	CALL	STRCPY

	MOV	ESI,FILE1
	MOV	EDI,STR_REL
	CALL	STRCAT

	MOV	EDX,FILE1
	MOV	AX,3D00H	;open
	CALL	INT21_open	;INT	21H
	;JB	ERR0
	JB	LINKFILE.OPEN_ERR
	MOV	[HDL],EAX

	MOV	EBX,0
	MOV	[S.LEFT],EBX
	XOR	AL,AL
	MOV	[FL.END],AL

	CALL	LINKMAIN

	MOV	AX,3E00H	;close
	MOV	EBX,[HDL]
	CALL	INT21_close	;INT	21H

	MOV	BYTE [NOT_FIRST_MODULE],1

	CLC
	RET

LINKFILE.E:
	STC
	RET

LINKFILE.OPEN_ERR:
	MOV	EDX,FILE1
	CALL	PUTS_CON
	JMP	ERR0
;
ALIGN_MODULE:
	CMP	BYTE [NOT_FIRST_MODULE],0
	JE	ALIGN_MODULE.R

	CMP	BYTE [FLG.A],0
	JE	ALIGN_MODULE.R
ALIGN_MODULE.10:
	MOV	EDX,[LOC]
	TEST	EDX,0FH
	JE	ALIGN_MODULE.R

	MOV	AL,0
	CALL	PUTCHR
	INC	DWORD [LOC]
	JMP	ALIGN_MODULE.10

ALIGN_MODULE.R:
	RET


;
PUTS_CON:
	MOV	AH,1DH
	MOV	AL,[EDX]
	OR	AL,AL
	JE	PUTS_CON.R
	PUSH	EDX
	CALL	INT91_1D	;INT	91H
	POP	EDX
	INC	EDX
	JMP	PUTS_CON
PUTS_CON.R:
	RET
;
;
;
LINKMAIN:
;	RET

	CALL	GETSYMS

;	RET

LINK.LP:
	CALL	GETCHR
	JB	LINK.EOF
	OR	AL,AL
	JNE	LINK.10

	CALL	GETCHR
	CALL	PUTCHR

	INC	DWORD [LOC]
	JMP	LINK.LP

LINK.ABSS:
	MOVZX	ECX,AL
LINK.ABSS.10:
	PUSH	ECX
	CALL	GETCHR
	JB	ERR6
	CALL	PUTCHR

	INC	DWORD [LOC]
	POP	ECX
	LOOP	LINK.ABSS.10

	JMP	LINK.LP

LINK.10:
	CMP	AL,ID_ABS_MAXLEN
	JBE	LINK.ABSS

	CMP	AL,ID_EXTRN
	JE	LINK#80
	CMP	AL,ID_EXTRN+1
	JE	LINK#80

	CMP	AL,ID_OFFSET
	JE	LINK#84

	CMP	AL,ID_EOF
	JE	LINK.EOF

	JMP	ERR6

LINK#80:
	MOV	[LINK_ID],AL

	MOV	AL,1
	CALL	GETEXTRN
	MOV	EBX,WORK1
	MOV	[PNT2],EBX
	CALL	SRCHL
	MOV	EBX,[PNT2]
	JNB	LINK#80.10

	MOV	AL,ID_EXTRN	;20H
	OR	AL,04H		;referd
	MOV	[ATTR],AL

	PUSH	DWORD [LBL.NX]

	MOV	DWORD [SET.LBL.PNT],WORK1
	MOV	EDX,[LBL.NX]
	CALL	SET.LBL
	MOV	[LBL.NX],EDX

	INC	DWORD [LBL.NO]

	MOV	EBX,[LBL.NX]
	CMP	EBX,EOM
	JNB	ERR8

	POP	EBX

LINK#80.10:
	PUSHAD
	XOR	EAX,EAX
	MOV	AL,[EBX]
	ADD	EAX,1+4+4
	ADD	EAX,EBX
	OR	BYTE [EAX],04H	;refered
	POPAD

	MOV	EAX,[LOC]
	MOV	EDX,[EXTRN_LINK_PNT]
	;CMP	EDX,EXTRN_LINK_END
	CMP	EDX,[EXTRN_LINK_END]
	JNB	ERR_MEM

	MOV	[EDX],EAX
	MOV	[EDX+4],EBX
	MOV	AL,[LINK_ID]
	MOV	[EDX+8],AL

        MOV     EAX,[VAL1]
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#

	ADD	DWORD [EXTRN_LINK_PNT],9
	ADD	DWORD [LOC],4

	JMP	LINK.LP

LINK#84:
	CALL	GETCHR#
	MOV	BL,AL
	CALL	GETCHR#
	MOV	BH,AL
	ROL	EBX,10H
	CALL	GETCHR#
	MOV	BL,AL
	CALL	GETCHR#
	MOV	BH,AL
	ROL	EBX,10H

	ADD	EBX,[TOP_LOC]
	MOV	EAX,EBX
	CALL	PUTD

	ADD	DWORD [LOC],4
	JMP	LINK.LP

;
;
;

PUTD:
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#
        SHR     EAX,8
        CALL    PUTCHR#
	RET


LINK.EOF:
	RET

	RET

;
PUTCHR_ERR:
	MOV	EDX,PUTCHR_ERR_BUF
	MOV	[EDX],AL
	MOV	AX,4000H
	MOV	BX,2
	MOV	ECX,1
	CALL	INT21_write	;INT	21H
	RET



;
SORT_EXTRN:
	MOV	AH,1DH
	MOV	AL,'*'
	CALL	INT91_1D	;INT	91H

	CMP	BYTE [FLG.S],0
	JE	SORT_EXTRN.R

SORT_EXTRN.L2:
	XOR	EDX,EDX
	MOV	ESI,[EXTRN_LINK_MEMORY]	;EXTRN_LINK
SORT_EXTRN.L1:
	CMP	ESI,[EXTRN_LINK_PNT]
	JNB	SORT_EXTRN.L1E

	MOV	EDI,ESI
	ADD	EDI,9
	CMP	EDI,[EXTRN_LINK_PNT]
	JNB	SORT_EXTRN.L1E

	MOV	EAX,[ESI]
	CMP	EAX,[EDI]
	JBE	SORT_EXTRN.L1N

	OR	EDX,1
	PUSH	ESI
	PUSH	EDI
	MOV	ECX,9
SORT_EXTRN.EX:
	MOV	AL,[ESI]
	MOV	AH,[EDI]
	MOV	[ESI],AH
	MOV	[EDI],AL
	INC	ESI
	INC	EDI
	LOOP	SORT_EXTRN.EX
	POP	EDI
	POP	ESI

SORT_EXTRN.L1N:
	ADD	ESI,9
	JMP	SORT_EXTRN.L1

SORT_EXTRN.L1E:
	OR	EDX,EDX
	JNE	SORT_EXTRN.L2

SORT_EXTRN.R:
	RET

;
PATCH_EXTRN:
	MOV	ESI,[EXTRN_LINK_MEMORY]	;EXTRN_LINK
PATCH_EXTRN.LP:
	CMP	ESI,[EXTRN_LINK_PNT]
	JNB	PATCH_EXTRN.E

		PUSHAD
		MOV	AH,1DH
		MOV	AL,'.'
		CALL	INT91_1D	;INT	91H
		POPAD

	PUSHAD
	MOV	EDX,[ESI]

		CMP	BYTE [FLG.E],0
		JE	PATCH_EXTRN.EXPE
		ADD	EDX,200H

PATCH_EXTRN.EXPE:
	SUB	EDX,00400000h	;;

	MOV	ECX,EDX
	SHR	ECX,10H
	MOV	AX,4200H
	MOV	EBX,[HDL2]
	CALL	INT21_seek	;INT	21H
	POPAD

	MOV	EBX,[ESI+4]
	XOR	ECX,ECX
	MOV	CL,[EBX]
	ADD	ECX,1+4
	ADD	EBX,ECX
	MOV	EAX,[EBX]

	MOV	DL,[ESI+8]
	TEST	DL,1
	JE	PATCH_EXTRN.10

	SUB	EAX,[ESI]
	SUB	EAX,4

PATCH_EXTRN.10:
	MOV	[PATCH_VAL],EAX

	PUSHAD
	MOV	AX,4000H
	MOV	EBX,[HDL2]
	MOV	ECX,4
	MOV	EDX,PATCH_VAL
	CALL	INT21_write	;INT	21H

	POPAD

	ADD	ESI,9
	JMP	PATCH_EXTRN.LP

PATCH_EXTRN.E:
	RET



;
;
;
CHECK_EXTRN:
	MOV	DX,[LBL.NO]
	OR	DX,DX
	JE	CHECK_EXTRN.E

	MOV	EBX,W.LBL

CHECK_EXTRN0:
	MOV	CH,[EBX]		;len

	MOVZX	EDI,CH
	ADD	EDI,EBX
	ADD	EDI,1+4+4
	MOV	AL,[EDI]

	TEST	AL,04H
	JE	CHECK_EXTRN.UNREF	;if not referd

	TEST	AL,80H
	JNE	CHECK_EXTRN.ERR


	JMP	CHECK_EXTRNS

CHECK_EXTRN.UNREF:
	PUSHAD
	MOV	EDX,EMES_EXTRN_UNREF
	MOV	AH,9
	CALL	INT21_putstr	;INT	21H
	POPAD
	JMP	CHECK_EXTRN.ERR.1

CHECK_EXTRN.ERR:
	PUSHAD
	MOV	EDX,EMES_UNDEF_EXTRN
	MOV	AH,9
	CALL	INT21_putstr	;INT	21H

	MOV	AH,1DH
	MOV	AL,'U'
	CALL	INT91_1D	;INT	91H
	POPAD

CHECK_EXTRN.ERR.1:
	PUSHAD
	MOV	ESI,EBX
	CALL	DISP_LBL2

	MOV	AL,0DH
	CALL	DOS02
	MOV	AL,0AH
	CALL	DOS02
	POPAD

CHECK_EXTRNS:
	MOV	EBX,EDI
	INC	EBX

CHECK_EXTRN.LE:
	DEC	DX
	OR	DX,DX
	JNE	CHECK_EXTRN0

	MOV	AL,0
CHECK_EXTRN.E:
	RET

;
DISP_LBL2:
	LODSB
	MOVZX	ECX,AL
	JCXZ	DISP_LBL2.R

	ADD	ESI,4	;for link
DISP_LBL2.10:
	LODSB
	PUSH	ECX
	PUSH	ESI
	CALL	DOS02
	POP	ESI
	POP	ECX
	LOOP	DISP_LBL2.10
DISP_LBL2.R:
	RET

EMES_EXTRN_UNREF:
	DB	'unrefered extrn $'

EMES_PUBLIC_UNDEF:
	DB	'undefined public $'

EMES_UNDEF_EXTRN:
	DB	'undefined extrn $'
;

FLUSH:
	MOV	EBX,W.BUF
	MOV	EDX,[W.LEFT]
	SUB	EBX,EDX
	JE	FLUSH.E

	MOV	ECX,EBX
	MOV	EDX,USRDMA+R.BUF
	MOV	EBX,[HDL2]
	MOV	AX,4000H	;write
	PUSH	ECX
	CALL	INT21_write	;INT	21H
	POP	ECX
	JB	ERR3

	CMP	EAX,ECX
	JNE	ERR3

FLUSH.E:
	RET



;
GETCHR#:
	PUSH	EBX
	PUSH	ECX
	PUSH	EDX
	PUSH	ESI
	PUSH	EDI
        CALL    GETCHR
	POP	EDI
	POP	ESI
	POP	EDX
	POP	ECX
	POP	EBX
        RET
;
PUTCHR#:
        PUSHAD
        CALL    PUTCHR
        POPAD
        RET

;
GETSYMS:
	MOV	AL,0
	CALL	GETEXTRN
	JB	GETSYMS.E

	MOV	AL,[ATTR]
	MOV	[SYMS_ATTR],AL

	MOVZX	ECX,BYTE [WORK1]
	ADD	ECX,WORK1+1;+4
	MOV	EAX,[ECX]
	MOV	[SYMS_VAL],EAX

	MOV	EBX,WORK1
	MOV	[PNT2],EBX
	CALL	SRCHL
	;JNB	ERR5		;multiply defined
	JNB	GETSYMS.10

	XOR	ECX,ECX
	MOV	CL,[WORK1]
	ADD	ECX,WORK1+1
	MOV	EAX,[ECX]
	TEST	BYTE [SYMS_ATTR],08H
	JNE	GETSYMS.1
	ADD	EAX,[TOP_LOC]
GETSYMS.1:
	MOV	[ECX],EAX

	;MOV	AL,[ECX+4]
	MOV	AL,[SYMS_ATTR]
	MOV	[ATTR],AL

	MOV	DWORD [SET.LBL.PNT],WORK1
	MOV	EDX,[LBL.NX]
	CALL	SET.LBL
	MOV	[LBL.NX],EDX

	INC	DWORD [LBL.NO]

	CMP	EDX,EOM
	JNB	ERR8

	JMP	GETSYMS

GETSYMS.10:
	MOV	EBX,[PNT2]
	XOR	ECX,ECX
	MOV	CL,[EBX]
	ADD	ECX,1+4+4
	ADD	EBX,ECX
	MOV	AL,[EBX]
	MOV	AH,AL

	MOV	AL,[SYMS_ATTR]
	TEST	AL,ID_PUBLIC
	JNE	GETSYMS#40

	TEST	AL,ID_EXTRN
	JNE	GETSYMS#80

	TEST	AL,20H
	;JE	ERR5
	JE	GETSYMS_MULT

	MOV	AL,ID_EXTRN
	MOV	[ATTR],AL

;	MOV	EAX,[SYMS_VAL]
;	ADD	EAX,[TOP_LOC]
;	MOV	[EBX+1],EAX
	JMP	GETSYMS

GETSYMS_MULT:
	MOV	AH,1DH
	MOV	AL,'*'
	;CALL	PUTCHR_CON
	CALL	INT91_1D	;INT	91H

	MOV	EBX,WORK1
	MOVZX	ECX,BYTE [EBX]
	INC	EBX
GETSYMS_MULT.10:
	PUSH	ECX
	MOV	AH,1DH
	MOV	AL,[EBX]
	;CALL	PUTCHR_CON
	CALL	INT91_1D	;INT	91H
	INC	EBX
	POP	ECX
	LOOP	GETSYMS_MULT.10

	JMP	GETSYMS

GETSYMS#80:
	JMP	GETSYMS

	MOV	[ATTR],AL

	MOV	EAX,[SYMS_VAL]
	ADD	EAX,[TOP_LOC]
	MOV	[EBX+1],EAX
	JMP	GETSYMS


GETSYMS#40:
	TEST	BYTE [ATTR],40H
	;JNE	ERR5
	JNE	GETSYMS_MULT

	MOV	[EBX],AL

		MOV	AL,[ATTR]
		AND	AL,04H		;referd
		OR	[EBX],AL

	MOV	EAX,[SYMS_VAL]
	TEST	BYTE [SYMS_ATTR],08H
	JNE	GETSYMS.2
	ADD	EAX,[TOP_LOC]
GETSYMS.2:
	MOV	[EBX-4],EAX
	JMP	GETSYMS

GETSYMS.E:
	RET



;

GETEXTRN:
	MOV	[GETEXTRN.FLG],AL

	MOV	EBX,WORK1

	PUSH	EBX
	CALL	GETCHR
	POP	EBX
	MOV	[EBX],AL
	OR	AL,AL
	JE	GETEXTRN.ZERO

	INC	EBX

	MOVZX	ECX,AL

	MOV	AL,[GETEXTRN.FLG]
	OR	AL,AL
	JNE	GETEXTRN.10

	ADD	ECX,4		;for val
GETEXTRN.10:
	PUSH	ECX
	PUSH	EBX
	CALL	GETCHR
	POP	EBX
	POP	ECX
	MOV	[EBX],AL
	INC	EBX
	LOOP	GETEXTRN.10

	MOV	AL,[GETEXTRN.FLG]
	OR	AL,AL
	JNE	GETEXTRN.20

	CALL	GETCHR		;attr
	MOV	[ATTR],AL
GETEXTRN.20:
	CLC
	RET

GETEXTRN.ZERO:
	STC
	RET


;

ASM:
		MOV	BYTE [COND],0

	XOR	AL,AL
	MOV	[LOAD.F],AL

	MOV	EBX,FILE1+80
	MOV	[FCB],EBX
	MOV	ESI,FILE1
	MOV	EDI,FILE1+80
	CALL	STRCPY

	MOV	CX,0
	MOV	DX,0
	MOV	AX,4200H	;seek
	MOV	EBX,[HDL]
	CALL	INT21_seek	;INT	21H


PNO:	MOV	EDX,MES2
	CALL	DOS09
	MOV	AL,[PASS]
	ADD	AL,'0'
	CALL	DOS02
	CALL	CRLF

	CMP	BYTE [PASS],2
	JNE	PNO.EXP
	CMP	BYTE [FLG.E],0
	JE	PNO.EXP

	MOV	AX,4000H
	MOV	EBX,[HDL2]
	MOV	ECX,200H
	MOV	EDX,USRDMA	;just for seek
	CALL	INT21_write	;INT	21H

PNO.EXP:
	MOV	DWORD [SETABS_BUF_PNT],SETABS_BUF	;##

	MOV	EBX,0000H
	MOV	[O.ADR1],EBX
	MOV	EBX,0000H
	MOV	[O.ADR2],EBX
	MOV	[O.BYTE],EBX

	MOV	BX,1
	;
	MOV	[LIN.NO],BX

	MOV	EBX,W.LBL
	MOV	[LBL.NX],EBX
	MOV	AL,[PASS]
	DEC	AL
	JNE	PNO2
	MOV	BX,0
	MOV	[LBL.NO],BX
	MOV	[ERR.NO],BX
PNO2:
	MOV	EBX,0
	MOV	[S.LEFT],EBX
	XOR	AL,AL
	MOV	[FL.END],AL

	CALL	PUT.P

	JMP	MAIN01

;

M10:
	MOV	ESI,L.DATA
	MOV	AL,[ESI]
	CMP	AL,09H
	JE	MAIN10
	CMP	AL,' '
	JE	MAIN10
	OR	AL,AL
	JE	MAIN50
	CMP	AL,';'
	JE	MAIN50
;

M11:	;CALL	UPPER
	CMP	AL,' '+1
	JB	ERR4
	CMP	AL,':'
	JE	ERR4
;
	MOV	EBX,WORK1
	XOR	AL,AL
	MOV	[EBX],AL
	INC	EBX
	MOV	CH,80+2		;40+2
	MOV	AL,' '
M10.1:	MOV	[EBX],AL
	INC	EBX
	DEC	CH
	JNE	SHORT M10.1

	MOV	EDX,WORK1+1
	MOV	CH,80		;40
LBLS10:	MOV	AL,[ESI]
	;CALL	UPPER
	CMP	AL,':'
	JE	LBLS17
	CMP	AL,09H
	JE	L.EQU
	CMP	AL,' '
	JE	L.EQU

		OR	AL,AL
		JE	SUBCOM2

;
	CMP	AL,';'
	JE	ERR4
	MOV	[EDX],AL	;CALL	LDDEA
	INC	ESI
	INC	EDX
	DEC	CH
	JNE	LBLS10

LBLS15:	MOV	AL,[ESI]
	;CALL	UPPER
	CMP	AL,':'
	JE	LBLS17
	CMP	AL,09H
	JE	L.EQU
	CMP	AL,' '
	JE	L.EQU
	CMP	AL,' '+1
	JB	ERR4
;
	CMP	AL,';'
	JE	ERR4
	INC	ESI
	JMP	LBLS15

LBLS17:
	MOV	BYTE [ATTR1],ID_DEFINED	;40H	attr=def	;attr = rel

	CMP	BYTE [ESI+1],':'
	JNE	LBLS17#
	INC	ESI

	MOV	BYTE [ATTR1],ID_PUBLIC	;attr = PUBLIC

LBLS17#:
	MOV	AL,80		;40
	SUB	AL,CH
	MOV	[WORK1],AL

	MOV	EBX,EDX
	MOV	EDX,[O.ADR1]
;	AND	EDX,0FFFFH	;#
	MOV	[EBX],EDX

	MOV	BYTE [IS_EQU],0
	JMP	LBLS25

;--------

L.EQU:	MOV	AL,80		;40
	SUB	AL,CH
	MOV	[WORK1],AL
	CALL	S.SKIP
	JE	ERR6
	MOV	CL,'E'
	CALL	L.EQU2
	MOV	CL,'Q'
	CALL	L.EQU2
	MOV	CL,'U'
	CALL	L.EQU2

;
	MOV	AL,[ESI]
	INC	ESI
	CMP	AL,09H
	JE	L.EQU1
	CMP	AL,' '
	JE	L.EQU1
	JMP	SUBCOM
L.EQU1:

	CALL	S.SKP2
	JE	ERR6

	PUSH	EDX
	CALL	GET.NO
	POP	EDX
;
	DEC	ESI
	CALL	S.SKIP
	JNE	ERR6
	DEC	ESI
	MOV	EBX,EDX
	MOV	EDX,[VAL1]
	MOV	[EBX],EDX

	;MOV	AL,00H		:attr = abs
	;MOV	AL,[ATTR]
	MOV	AL,[ATTR1]
	OR	AL,ID_DEFINED
	;MOV	[EBX+4],AL
	MOV	[ATTR1],AL

	MOV	BYTE [IS_EQU],0FFH
	JMP	LBLS25





;

LBLS25:	MOV	EBX,WORK1
	MOV	[PNT2],EBX
	CALL	SRCHL
	JB	LBLS32

		MOV	AL,[ATTR]
		TEST	AL,ID_EXTRN
		JNE	LBLS#80

		OR	AL,[ATTR1]
		MOV	[ATTR1],AL
		JMP	LBLS.E
LBLS#80:
;		MOV	AL,[ATTR]
;		MOV	[ATTR1],AL

LBLS.E:

		TEST	AL,ID_EXTRN
		JNE	LBLS25.10
		TEST	AL,ID_PUBLIC
		JNE	LBLS25.10

	MOV	AL,[PASS]
	DEC	AL
	JE	ERR5		;Multiply defined label

LBLS25.10:
	CLC
	MOV	EDX,[PNT2]	;set again
	PUSHFD
;	JMP	LBLS35

		POPFD
		XOR	ECX,ECX
		MOV	CL,[EDX]
		ADD	ECX,1+4
		ADD	ECX,EDX

		XOR	EAX,EAX
		MOV	AL,[WORK1]
		ADD	EAX,WORK1+1
		MOV	EAX,[EAX]	;val

		MOV	[ECX],EAX
		MOV	AL,[ATTR1]
		MOV	[ECX+4],AL

		JMP	LBLS36

LBLS32:	STC
	PUSHFD
	MOV	EDX,[LBL.NX]	;newly set

;

LBLS35:


		MOV	AL,[ATTR1]
		MOV	[ATTR],AL

	MOV	DWORD [SET.LBL.PNT],WORK1
	CALL	SET.LBL

	POPFD
	JNB	LBLS36		;if set again

	MOV	[LBL.NX],EDX

	INC	WORD [LBL.NO]

	CMP	DWORD [LBL.NX],EOM		;0D000H end of memory
	JNB	ERR8

	CMP	BYTE [PASS],1
	JE	LBLS36

	MOV	AL,'&'
	CALL	DOS02

		PUSHAD
		MOV	ESI,WORK1
		CALL	DISP_LBL
		POPAD

	JMP	LBLS36

DISP_LBL:
	LODSB
	MOVZX	ECX,AL
	JCXZ	DISP_LBL.R
DISP_LBL.10:
	LODSB
	PUSH	ECX
	PUSH	ESI
	CALL	DOS02
	POP	ESI
	POP	ECX
	LOOP	DISP_LBL.10
DISP_LBL.R:
	RET







;*************************************

OP.TBL:	DB	8
	DD	PNT.A
	DB	2
	DD	PNT.B
	DB	12
	DD	PNT.C
	DB	13
	DD	PNT.D
	DB	1+1	;+EXTRN
	DD	PNT.E
	DB	0
	DD	0	;F
	DB	0
	DD	0	;G
	DB	0
	DD	PNT.H
	DB	6
	DD	PNT.I
	DB	30
	DD	PNT.J
	DB	0
	DD	0	;K
	DB	8
	DD	PNT.L

	DB	7
	DD	PNT.M	;M

	DB	3+1	;+native
	DD	PNT.N
	DB	3
	DD	PNT.O
	DB	10+1	;+PUBLIC
	DD	PNT.P
	DB	0
	DD	0	;Q
	DB	10+1	;real
	DD	PNT.R
	DB	16+16	;+SETCC
	DD	PNT.S
	DB	1
	DD	PNT.T	;T
	DB	0
	DD	0	;U
	DB	0
	DD	0	;V
	DB	0
	DD	0	;W
	DB	3
	DD	PNT.X
	DB	0
	DD	0	;Y
	DB	0
	DD	0	;Z






PNT.E:	DB	'ND   '
	DD	C.END

	DB	'XTRN '
	DD	C.EXTRN









PNT.P:	DB	'USH  '
	DD	C.PUSH
	DB	'OP   '
	DD	C.POP
	DB	'USHF '
	DD	C.PUSHF
	DB	'OPF  '
	DD	C.POPF
	DB	'USHFD'
	DD	C.PUSHFD
	DB	'OPFD '
	DD	C.POPFD
	DB	'USHA '
	DD	C.PUSHA
	DB	'OPA  '
	DD	C.POPA
	DB	'USHAD'
	DD	C.PUSHAD
	DB	'OPAD '
	DD	C.POPAD

	DB	'UBLIC'
	DD	C.PUBLIC






;J.TBL1:


;*************************************

C.PUBLIC:
	MOV	AL,ID_PUBLIC
	JMP	C.ATTR

C.EXTRN:
	MOV	AL,ID_EXTRN
	JMP	C.ATTR

C.ATTR:
	MOV	[C.ATTR_AL],AL

	CALL	S.SKP2
	JE	ERR6

	CALL	GETNAME
	CALL	S.SKP2
	JNE	ERR6
	MOV	[PNT],ESI

	MOV	[PNT2],EBX
	CALL	SRCHL
	JNB	C.EXTRN.20

	MOV	AL,[C.ATTR_AL]
	MOV	[ATTR],AL

	MOV	EBX,WORK1
	MOV	EDX,[LBL.NX]
	MOV	DWORD [SET.LBL.PNT],WORK1
	CALL	SET.LBL
	MOV	[LBL.NX],EDX
	INC	DWORD [LBL.NO]

	MOV	EBX,[LBL.NX]
	CMP	EBX,EOM
	JNB	ERR8
	JMP	MAIN20

C.EXTRN.20:
	MOV	EBX,[PNT2]
	MOVZX	ECX,BYTE [EBX]	
	ADD	ECX,1+4+4
	ADD	EBX,ECX

	MOV	AL,[C.ATTR_AL]
	CMP	AL,ID_PUBLIC
	JE	C.ATTR.PUBLIC

	MOV	AL,[EBX]
	TEST	AL,ID_PUBLIC
	JNE	C.ATTR.EXTRN.10

	OR	AL,[C.ATTR_AL]
	MOV	[EBX],AL
	JMP	MAIN20

C.ATTR.EXTRN.10:
	JMP	MAIN20

C.ATTR.PUBLIC:
	MOV	AL,[EBX]
	AND	AL,7FH
	OR	AL,ID_PUBLIC
	MOV	[EBX],AL
	JMP	MAIN20



;
;
;

GETNAME:
	MOV	EDX,WORK1+1

	MOV	CH,40

_GETN05:
	MOV	AL,[ESI]
	CMP	AL,';'
	JE	_GETN20
	OR	AL,AL
	JE	_GETN20
	CMP	AL,09H
	JE	_GETN20
	CMP	AL,' '
	JE	_GETN20
	CMP	AL,','
	JE	_GETN20
	CMP	AL,'+'
	JE	_GETN20
	CMP	AL,'-'
	JE	_GETN20
	CMP	AL,'*'
	JE	_GETN20
	CMP	AL,'/'
	JE	_GETN20
	CMP	AL,'['	;#
	JE	_GETN20
	CMP	AL,']'
	JE	_GETN20
	CMP	AL,'}'
	JE	_GETN20
_GETN06:
	;CALL	UPPER
	MOV	[EDX],AL	;CALL	LDDEA
	INC	ESI
	INC	EDX
	DEC	CH
	JNE	_GETN05
	JMP	ERR6

_GETN20:
	MOV	AL,' '
	MOV	[EDX],AL	;CALL	LDDEA

		MOV	DWORD [EDX],12345678H

	MOV	AL,40
	SUB	AL,CH
	MOV	[WORK1],AL

	MOV	EBX,WORK1

	RET

;
;
;
C.DD:	CALL	OPCODE
	CMP	AL,50H
	JNE	ERRB
C.DD1:
	;MOV	EBX,[VAL1]
	;CALL	SETD4
		MOV	AL,1
		CALL	SETV1

	DEC	ESI
	CALL	S.SKIP
	JE	MAIN20
	CALL	OPCOD2
	JMP	C.DD1











;
;
;
TERM2:	CALL	OPCODE

TERM2.SUB:
	MOV	CH,AL
	MOV	AL,[PRE_SIZE]
	MOV	[PRE_SIZE1],AL
	PUSH	ECX

	MOV	EBX,[VAL1]
	PUSH	EBX

	PUSH	DWORD [NAME_PNT1]
	MOV	BL,[ATTR1]
	PUSH	EBX

	CALL	OPCOD2

		MOV	EBX,[NAME_PNT1]
		MOV	[NAME_PNT2],EBX
		MOV	BL,[ATTR1]
		MOV	[ATTR2],BL

	POP	EBX
	MOV	[ATTR1],BL
	POP	DWORD [NAME_PNT1]

	MOV	BL,[PRE_SIZE]
	;MOV	[PRE_SIZE2],AL
		MOV	[PRE_SIZE2],BL	;###
	MOV	EBX,[VAL1]
	MOV	[VAL2],EBX

	POP	EBX
	MOV	[VAL1],EBX

	POP	ECX
	MOV	CL,AL
	RET




;	AL:30H+00 mod r/m
;	DL:00 ??? 000
;
	DB	'MEM'

MEM:
	PUSH	ECX
	PUSH	EBX
	PUSH	EAX

	CALL	CVMOD
	OR	AL,DL
	CALL	SETDAT
	CALL	IS386
	JE	MEM_3

	AND	AL,0C7H
	CMP	AL,00000110B	;[nn]
	JE	MEM.W
	AND	AL,0C0H
	CMP	AL,40H
	JE	MEM.B
	CMP	AL,80H
	JE	MEM.W
	JMP	MEM.E

MEM_3:	CMP	AL,10000101B	;[EBP+nnnn]
	JE	MEM.D
	CMP	AL,01000101B	;[EBP+n]
	JE	MEM.B

	AND	AL,0C7H
	CMP	AL,00000101B	;[nnnn]
	JE	MEM.D
	CMP	AL,00000100B	;[+0]
	JE	MEM.0

	CMP	AL,10000100B	;[+nnnn]
	JE	MEM.0.N32
	AND	AL,0C0H
	CMP	AL,40H
	JE	MEM.B
	CMP	AL,80H
	JE	MEM.D
	JMP	MEM.E

MEM.0:	MOV	AL,[MOD2]
	MOV	BL,AL
	AND	BL,07H
	CMP	BL,101B	;nnnn[]
	JE	MEM.0.10
	CALL	SETDAT
	JMP	MEM.E
MEM.0.10:
	CALL	SETDAT
	JMP	MEM.D

;MEM.0.N8:
;	MOV	AL,[MOD2]
;	CALL	SETDAT
;	JMP	MEM.B

MEM.0.N32:
	MOV	AL,[MOD2]
	CALL	SETDAT
	JMP	MEM.D

MEM.B:	MOV	AL,[ESI]
	CALL	SETDAT
	JMP	MEM.E

MEM.W:	MOV	BX,[ESI]		;VAL1/VAL2
	CALL	SETD2
	JMP	MEM.E

MEM.D:
	;MOV	BX,[SI]
	;CALL	SETD2
	;INC	ESI
	;INC	ESI
	;MOV	BX,[SI]
	;CALL	SETD2

	MOV	AL,[ESI-4]
	TEST	AL,80H
	JNE	MEM.D.EXTRN

	TEST	AL,08H
	JE	MEM.D.OFFSET

	MOV	EBX,[ESI]
	CALL	SETD4
	JMP	MEM.E

MEM.D.EXTRN:
	PUSHAD
	MOV	AL,0
	MOV	EBX,[ESI+4]
	CALL	SETEXTRN
	POPAD
	JMP	MEM.E

MEM.D.OFFSET:
	MOV	EBX,[ESI]
	CALL	SETOFFSET
MEM.E:
	POP	EAX
	POP	EBX
	POP	ECX
	RET

;

SETV1:	PUSH	ESI
	PUSH	EBX
	PUSH	EAX
	MOV	ESI,VAL1
	JMP	SETVAL

SETV2:	PUSH	ESI
	PUSH	EBX
	PUSH	EAX
	MOV	ESI,VAL2
	JMP	SETVAL

;

SETVAL:	TEST	AL,01H
	JNE	SETVAL.10

	MOV	AL,[ESI]
	CALL	SETDAT
	JMP	SETVAL.E
SETVAL.10:
	CALL	ISDWORD
	JE	SETVAL.15
	MOV	BX,[ESI]
	CALL	SETD2
	JMP	SETVAL.E
SETVAL.15:
	;MOV	BX,[SI]
	;CALL	SETD2
	;INC	ESI
	;INC	ESI
	;MOV	BX,[SI]
	;CALL	SETD2

	MOV	AL,[ESI-4]
	TEST	AL,80H
	JNE	SETVAL.EXTRN

	TEST	AL,08H
	JE	SETVAL.OFFSET

	MOV	EBX,[ESI]
	CALL	SETD4
	JMP	SETVAL.E

SETVAL.EXTRN:
	MOV	AL,0
	MOV	EBX,[ESI+4]
	CALL	SETEXTRN
	JMP	SETVAL.E

SETVAL.OFFSET:
	MOV	EBX,[ESI]
	CALL	SETOFFSET

SETVAL.E:
	POP	EAX
	POP	EBX
	POP	ESI
	RET

;

SETA1:	PUSH	ESI
	PUSH	EBX
	PUSH	EAX
	MOV	ESI,VAL1
	JMP	SETADRS

SETA2:	PUSH	ESI
	PUSH	EBX
	PUSH	EAX
	MOV	ESI,VAL2
	JMP	SETADRS

;
CALREL:
	MOV	AL,[ATTR1]
	TEST	AL,ID_EXTRN
	JNE	CALREL.EXTRN

	MOV	EBX,[VAL1]
	MOV	EDX,[O.ADR1]
	INC	EDX
	INC	EDX
	CALL	ISDWORD
	JE	CALREL.10
	SUB	EBX,EDX
	RET
CALREL.10:
	INC	EDX
	INC	EDX
	SUB	EBX,EDX
	RET
;
CALREL.EXTRN:
	MOV	EBX,1000H
	RET

;

RELADRS:
	MOV	AL,[ATTR1]
	TEST	AL,ID_EXTRN
	JNE	RELADRS.EXTRN

	MOV	EBX,[VAL1]
	MOV	EDX,[O.ADR1]
	INC	EDX
	INC	EDX
	CALL	ISDWORD
	JE	RELADRS.10
	SUB	EBX,EDX
	CALL	SETD2
	RET
RELADRS.10:
	INC	EDX
	INC	EDX
	SUB	EBX,EDX

		CALL	SETD4
		RET

	PUSH	EBX

	CALL	SETD2

	POP	EBX
	SHR	EBX,10H

	CALL	SETD2
	RET

;
RELADRS.EXTRN:
	MOV	AL,1
	MOV	EBX,[NAME_PNT1]
	CALL	SETEXTRN

	RET

;
SETOFFSET:
	CALL	FLUSH_ABS	;##

	PUSHAD
	MOV	AL,84H
	CALL	SETDAT0

	MOV	AL,BL
	CALL	SETDAT0
	MOV	AL,BH
	CALL	SETDAT0

	SHR	EBX,10H
	MOV	AL,BL
	CALL	SETDAT0
	MOV	AL,BH
	CALL	SETDAT0

	MOV	EBX,4
	CALL	ADD_ADRS
	POPAD
	RET

;
;
;

SETEXTRN:
	CALL	FLUSH_ABS	;##

	PUSHAD

	CMP	BYTE [PASS],1
	JE	SETEXTRN.E

	;MOV	AL,80H
	OR	AL,80H
	CALL	SETDAT0

	MOV	AL,[EBX]
	MOVZX	ECX,AL

	CALL	SETDAT0

	ADD	EBX,1+4
SETEXTRN.10:
	MOV	AL,[EBX]
	CMP	BYTE [FLG.CL],0
	JE	SETEXTRN.11
	CALL	LOWER
SETEXTRN.11:
	CALL	SETDAT0
	INC	EBX
	LOOP	SETEXTRN.10

SETEXTRN.E:
	MOV	EBX,4
	CALL	ADD_ADRS

	POPAD
	RET


;

SETADRS:
	CALL	IS386
	JE	SETADRS.15
	MOV	BX,[ESI]
	CALL	SETD2
	JMP	SETADRS.E
SETADRS.15:
	;MOV	BX,[SI]
	;CALL	SETD2
	;INC	ESI
	;INC	ESI
	;MOV	BX,[SI]
	;CALL	SETD2

	MOV	AL,[ESI-4]
	TEST	AL,80H
	JNE	SETADRS.EXTRN

	TEST	AL,08H
	JE	SETADRS.OFFSET

	MOV	EBX,[ESI]
	CALL	SETD4
	JMP	SETADRS.E

SETADRS.EXTRN:
	MOV	AL,0
	MOV	EBX,[ESI+4]
	CALL	SETEXTRN
	JMP	SETADRS.E

SETADRS.OFFSET:
	MOV	EBX,[ESI]
	CALL	SETOFFSET

SETADRS.E:
	POP	EAX
	POP	EBX
	POP	ESI
	RET


;	set AL

SETD1:	CALL	SETDAT
	JMP	MAIN20



;
STRCASECMP:
	PUSH	EAX
	PUSH	ECX
	CLD
	;MOV	CH,0
	XOR	ECX,ECX
	MOV	CL,[EDI]
	INC	EDI
STRCASECMP.10:
	MOV	AL,[ESI]
	CALL	UPPER
	CMP	AL,[EDI]
	JNE	STRCASECMP.R
	INC	ESI
	INC	EDI
	LOOP	STRCASECMP.10
	XOR	EAX,EAX
STRCASECMP.R:
	POP	ECX
	POP	EAX
	RET


	

;

CODE10:	MOV	[PNT],ESI
	MOV	EDX,WORK2+1
	MOV	CH,80		;40
	MOV	AL,[ESI]

		CMP	AL,'_'
		JE	SHORT CD90

	CMP	AL,'9'+1
	JNB	CODE11
	CMP	AL,'+'	;#
	JE	CD90
	CMP	AL,'-'
	JE	CD90
	CMP	AL,27H
	JE	CD90
	CMP	AL,'$'
	JE	CD90
	CMP	AL,'0'
	JB	CODE11
CD90:	CALL	GET.NO
	MOV	CH,50H
	JMP	CODE90

CODE11:	MOV	AL,[ESI]
	CMP	AL,'}'
	JE	CODE12
	CMP	AL,'['
	JE	CODE12	;#
	CMP	AL,']'
	JE	CODE12
	CMP	AL,'@'
	JNB	CDE110
	OR	AL,AL
	JE	CODE12
	CMP	AL,09H
	JE	CODE12
	CMP	AL,' '
	JE	CODE12
	CMP	AL,';'
	JE	CODE12
	CMP	AL,','
	JE	CODE12
	CMP	AL,'*'
	JE	CODE12	;#
	CMP	AL,'+'
	JE	CODE12
	CMP	AL,'-'
	JE	CODE12
CDE110:	;CALL	UPPER
	MOV	[EDX],AL	;CALL	LDDEA
	INC	ESI
	INC	EDX
	DEC	CH
	JNE	CODE11
	JMP	ERR6
CODE12:	MOV	AL,80		;40
	SUB	AL,CH
	MOV	[LEN],AL

CODE02:	MOV	CL,[LEN]

	CMP	CL,0
	JE	ERR6
;

CODE15:
	CMP	CL,3
	JNB	CODEL3

	MOV	EBX,C.DAT2
	MOV	CH,00H
	;MOV	EDX,WORK2+1
	MOV	AX,[WORK2+1]
		CALL	UPPER
		ROR	EAX,8
		CALL	UPPER
		ROL	EAX,8
CODE16:
	;MOV	AX,[EDX]	;CALL	LDADE
	CMP	AX,[EBX]	;DB	3AH,07H
	JE	CODE18.50

CODE18:	INC	EBX
	INC	EBX
	INC	CH
	CMP	CH,14H+2	;add FS,GS
	JNE	CODE16

	JMP	CODEL3


CODE18.50:
	CMP	CH,08H
	MOV	AL,1
	JB	CODE18.52
	MOV	AL,2
CODE18.52:
	MOV	[PRE_SIZE],AL

	CMP	CH,08H
	JB	CODE18.54
	CMP	CH,10H
	JNB	CODE18.54
	MOV	AL,00H
	MOV	[FLG2.DWORD],AL
CODE18.54:
	JMP	CODE20
;--------------
CODEL3:
	CMP	CL,4
	JNB	CODEL4

	CMP	BYTE [WORK2+3],':'
	JNE	CODEL3.SEG.E

	MOV	AX,[WORK2+1]
		CALL	UPPER
		ROR	EAX,8
		CALL	UPPER
		ROL	EAX,8
	CMP	AX,'C'+'S'*100H	;'SC'
	MOV	BL,2EH
	JE	CODEL3.SEG.10
	CMP	AX,'D'+'S'*100H	;'SD'
	MOV	BL,3EH
	JE	CODEL3.SEG.10
	CMP	AX,'E'+'S'*100H	;'SE'
	MOV	BL,26H
	JE	CODEL3.SEG.10
	CMP	AX,'S'+'S'*100H	;'SS'
	MOV	BL,36H
	JE	CODEL3.SEG.10
	JMP	CODEL3.SEG.E

CODEL3.SEG.10:
	MOV	[PRE_SEG],BL
	CALL	OPCODE
	RET




CODEL3.SEG.E:

	MOV	EBX,C.DAT3	;EAX..
	MOV	CH,08H
	;MOV	EDX,WORK2+1
	MOV	EAX,[WORK2+1]
	AND	EAX,00FFFFFFH
		CALL	UPPER
		ROR	EAX,8
		CALL	UPPER
		ROR	EAX,8
		CALL	UPPER
		ROL	EAX,16
CODEL3.10:
	MOV	EDX,[EBX]
	AND	EDX,00FFFFFFH
	CMP	EDX,EAX
	JE	CODEL3.50

CODEL3.20:
	INC	EBX
	INC	EBX
	INC	EBX
	INC	CH
	CMP	CH,10H
	JNE	CODEL3.10

	PUSH	ESI
	PUSH	EDI
	MOV	ESI,WORK2+1
	MOV	EDI,STR_FAR
	CALL	STRCASECMP	;STRCMP
	POP	EDI
	POP	ESI
	JE	CODE_FAR

	JMP	CODEL4




CODEL3.50:
	MOV	AL,0FFH
	MOV	[FLG2.DWORD],AL
	MOV	AL,4
	MOV	[PRE_SIZE],AL
	JMP	CODE20

;

CODEL4:

	CMP	CL,5
	JNB	CODEL5

	PUSH	ESI
	PUSH	EDI
	MOV	ESI,WORK2+1
	MOV	EDI,STR_BYTE
	CALL	STRCASECMP	;STRCMP
	POP	EDI
	POP	ESI
	JE	CODE_BYTE

	PUSH	ESI
	PUSH	EDI
	MOV	ESI,WORK2+1
	MOV	EDI,STR_WORD
	CALL	STRCASECMP	;STRCMP
	POP	EDI
	POP	ESI
	JE	CODE_WORD

	JMP	CODE19B



CODEL5:
	CMP	CL,6
	JNB	CODE19B

	PUSH	ESI
	PUSH	EDI
	MOV	ESI,WORK2+1
	MOV	EDI,STR_DWORD
	CALL	STRCASECMP	;STRCMP
	POP	EDI
	POP	ESI
	JE	CODE_DWORD

	PUSH	ESI
	PUSH	EDI
	MOV	ESI,WORK2+1
	MOV	EDI,STR_SHORT
	CALL	STRCASECMP	;STRCMP
	POP	EDI
	POP	ESI
	JE	CODE_SHORT

	JMP	CODE19B



;----------------

CODE19B:
	MOV	BYTE [ATTR1],ID_ABS

	XOR	EBX,EBX
	MOV	[VAL1],EBX
	MOV	ESI,[PNT]
	CALL	GET.NO
CODE1A:
	DEC	ESI
	CALL	L.SKIP
	MOV	CH,50H
	JMP	CODE90

CODE20:

CODE90:	MOV	AL,CH
	OR	AL,AL
	RET
CODE99:	MOV	AL,0FFH
	STC
	RET





;
;
;
PUTLIN:	MOV	EBX,L.DATA
LOOP2:
	CMP	EBX,[PNT]
	JNE	PUTLIN.10
	MOV	AL,'!'
	CALL	DOS02
PUTLIN.10:
	MOV	AL,[EBX]
	OR	AL,AL
	JE	_RET
	CMP	AL,1AH
	JE	_RET
	INC	EBX
	CALL	DOS02
	JMP	LOOP2

;
;	AL:code
;
;
;	?none
;
	ALIGN	4

SETDAT:
	JMP	SETABS_B	;##
	JMP	SETABS

;
SETDAT0:
;	INC	DWORD [O.ADR2]

	CMP	BYTE [PASS],1
	JE	SETDAT.10

	pushaD
	CALL	PUTCHR
	popaD
SETDAT.10:
;	INC	DWORD [O.ADR1]

	INC	DWORD [O.BYTE]
	RET

;
SETABS:
	PUSHAD

	PUSH	EAX
	MOV	AL,00H
	CALL	SETDAT0
	POP	EAX
	PUSH	EAX
	CALL	SETDAT0
	POP	EAX

	INC	DWORD [O.ADR2]
	INC	DWORD [O.ADR1]

	POPAD
	RET
;
SETABS_B:
	PUSHAD

	CMP	DWORD [SETABS_BUF_PNT],SETABS_BUF+0FH
	JB	SETABS_B.10

	PUSH	EAX
	CALL	FLUSH_ABS
	POP	EAX

	MOV	DWORD [SETABS_BUF_PNT],SETABS_BUF

SETABS_B.10:
	MOV	EBX,[SETABS_BUF_PNT]
	MOV	[EBX],AL
	INC	EBX
	MOV	[SETABS_BUF_PNT],EBX

	INC	DWORD [O.ADR2]
	INC	DWORD [O.ADR1]

	POPAD
	RET




;
FLUSH_ABS:
	PUSHAD

	CMP	BYTE [FLG.Q],0
	JNE	FLUSH_ABS.12

	MOV	EBX,SETABS_BUF
FLUSH_ABS.10:
	CMP	EBX,[SETABS_BUF_PNT]
	JNB	FLUSH_ABS.20

	MOV	AL,00H
	CALL	SETDAT0
	MOV	AL,[EBX]
	CALL	SETDAT0
	INC	EBX
	JMP	FLUSH_ABS.10


FLUSH_ABS.12:
	MOV	ECX,[SETABS_BUF_PNT]
	SUB	ECX,SETABS_BUF
	JE	FLUSH_ABS.20
	MOV	AL,CL
	CALL	SETDAT0

	MOV	EBX,SETABS_BUF
FLUSH_ABS.15:
	MOV	AL,[EBX]
	CALL	SETDAT0
	INC	EBX
	LOOP	FLUSH_ABS.15

FLUSH_ABS.20:
	MOV	DWORD [SETABS_BUF_PNT],SETABS_BUF

	POPAD
	RET


;
ADD_ADRS:
	ADD	[O.ADR2],EBX
	ADD	[O.ADR1],EBX
	RET




;-------------------------
GET.NO:
	MOV	BYTE [ATTR1],ID_ABS

	XOR	EBX,EBX
	MOV	[VAL1],EBX
	CALL	GET.NO_ADD
	MOV	[VAL1],EBX
	OR	AL,AL
	RET

GET.NO_ADD:
	CALL	GET.NO_MULT
GET.NO_ADD.1:
	MOV	AL,[ESI]
	CMP	AL,'+'
	JNE	GET.NO_SUB
	INC	ESI
	PUSH	EBX
	CALL	CHECK_EXT
	CALL	GET.NO_MULT
	CALL	CHECK_EXT
	POP	EDX
	ADD	EBX,EDX
	JMP	GET.NO_ADD.1
GET.NO_SUB:
	CMP	AL,'-'
	JNE	_RET
	INC	ESI
	PUSH	EBX
	CALL	CHECK_EXT
	CALL	GET.NO_MULT
	CALL	CHECK_EXT
	POP	EDX
	XCHG	EDX,EBX
	SUB	EBX,EDX
	JMP	GET.NO_ADD.1
;
;
GET.NO_MULT:
	CALL	GET.NO_FACT
GET.NO_MULT.1:
	MOV	AL,[ESI]
	CMP	AL,'*'
	JNE	GET.NO_DIV
	INC	ESI
	PUSH	EBX
	CALL	CHECK_EXT
	CALL	GET.NO_FACT
	CALL	CHECK_EXT
	POP	EDX
	CALL	MULT_HLDE
	JMP	GET.NO_MULT.1
GET.NO_DIV:
	CMP	AL,'/'
	JNE	_RET
	INC	ESI
	PUSH	EBX
	CALL	CHECK_EXT
	CALL	GET.NO_FACT
	CALL	CHECK_EXT
	XCHG	EDX,EBX
	POP	EBX
	CALL	DIV_HLDE
	JMP	GET.NO_MULT.1

GET.NO_FACT:
	MOV	AL,[ESI]
	CMP	AL,'+'
	JNE	GET.NO_FACT1
	INC	ESI
	JMP	GET.NO_FACT2
GET.NO_FACT1:
	CMP	AL,'-'
	JNE	GET.NO_FACT2
	INC	ESI
	CALL	CHECK_EXT
	CALL	GET.NO_FACT2
	CALL	CHECK_EXT
	NEG	EBX
	RET

GET.NO_FACT2:
	MOV	AL,[ESI]
	CMP	AL,'{'
	JNE	GET.NO_FACT3
	INC	ESI
	CALL	GET.NO_ADD
	MOV	AL,[ESI]
	CMP	AL,'}'
	JNE	ERR6
	INC	ESI
	OR	AL,AL
	RET

;
CHECK_EXT:
	RET

	CMP	BYTE [PASS],1
	JE	CHECK_EXT.R

;	TEST	BYTE [ATTR1],80H
;	JNE	ERR7

	PUSH	EAX
	MOV	AL,[ATTR1]
	TEST	AL,ID_EXTRN
	JNE	ERR7
	POP	EAX

CHECK_EXT.R:
	RET

;
;
GET.NO_FACT3:

GET.NO_FACT5:
	MOV	AL,[ESI]
	CMP	AL,' '
	JNE	GET.NO_FACT6
	INC	ESI
	JMP	GET.NO_FACT5

GET.NO_FACT6:

	MOV	EDX,WORK2+1
GN10:	MOV	AL,[ESI]
	CMP	AL,27H
	JE	GETN60
	CMP	AL,'"'
	JE	GETN60
	CMP	AL,'$'
	JE	GETN70

	MOV	CH,80	;40
GETN05:	MOV	AL,[ESI]
	CMP	AL,';'
	JE	GETN20
	OR	AL,AL
	JE	GETN20
	CMP	AL,09H
	JE	GETN20
	CMP	AL,' '
	JE	GETN20
	CMP	AL,','
	JE	GETN20
	CMP	AL,'+'
	JE	GETN20
	CMP	AL,'-'
	JE	GETN20
	CMP	AL,'*'
	JE	GETN20
	CMP	AL,'/'
	JE	GETN20
	CMP	AL,'['	;#
	JE	GETN20
	CMP	AL,']'
	JE	GETN20
	CMP	AL,'}'
	JE	GETN20
GETN06:	;CALL	UPPER
	MOV	[EDX],AL	;CALL	LDDEA
	INC	ESI
	INC	EDX
	DEC	CH
	JNE	GETN05
	JMP	ERR6

GETN20:	MOV	AL,' '
	MOV	[EDX],AL	;CALL	LDDEA
	MOV	AL,80	;40
	SUB	AL,CH
	MOV	[WORK2],AL

	CMP	AL,0
	JE	ERR6

	MOV	EBX,WORK2+1
	MOV	AL,[EBX]
	CMP	AL,'0'
	JB	GETN40
	CMP	AL,'9'+1
	JNB	GETN40
	XOR	AL,AL
	MOV	[EDX],AL	;CALL	LDDEA
	DEC	EDX
	MOV	AL,[EDX]	;CALL	LDADE

	XOR	EBX,EBX	;value
	CALL	UPPER		;##+
	CMP	AL,'H'
	JNE	GETN24

;	hex

	XOR	AL,AL
	MOV	[EDX],AL	;CALL	LDDEA
	MOV	EDX,WORK2+1

GETN21:	MOV	AL,[EDX]	;CALL	LDADE
	OR	AL,AL
	JE	GETN55
		CALL	UPPER
	CMP	AL,'A'
	JB	SHORT GETN22
	SUB	AL,7

GETN22:	SUB	AL,'0'
	ADD	EBX,EBX
	JB	ERR7
	ADD	EBX,EBX
	JB	ERR7
	ADD	EBX,EBX
	JB	ERR7
	ADD	EBX,EBX
	JB	ERR7

	MOVZX	EAX,AL
	ADD	EBX,EAX
	JB	ERR7
	INC	EDX
	JMP	GETN21

;	bin

GETN24:	CMP	AL,'B'
	JNE	GETN28
	XOR	AL,AL
	MOV	[EDX],AL	;CALL	LDDEA
	MOV	EDX,WORK2+1
	XOR	EBX,EBX

GETN25:	MOV	AL,[EDX]	;CALL	LDADE
	OR	AL,AL
	JE	GETN55
	SUB	AL,'0'
	CMP	AL,2
	JNB	ERR7
	ADD	EBX,EBX
	JB	ERR7
	XOR	ECX,ECX
	MOV	CL,AL
	ADD	EBX,ECX
	JB	ERR7
	INC	EDX
	JMP	GETN25

;	decimal

GETN28:	MOV	EDX,WORK2+1
	XOR	EBX,EBX
GETN29:	MOV	AL,[EDX]	;CALL	LDADE
	OR	AL,AL
	JE	GETN55
	CMP	AL,'9'+1
	JNB	ERR7
	SUB	AL,'0'
	ADD	EBX,EBX
	JB	ERR7
	MOV	ECX,EBX
	ADD	EBX,EBX
	JB	ERR7
	ADD	EBX,EBX
	JB	ERR7
	ADD	EBX,ECX
	JB	ERR7

	MOVZX	EAX,AL
	ADD	EBX,EAX
	JB	ERR7
	INC	EDX
	JMP	GETN29

GETN40:
		MOV	BYTE [INCL_NAME],1

	MOV	EBX,WORK2
	MOV	[PNT2],EBX

	;MOV	AL,[ATTR1]
	;PUSH	EAX

	MOV	EBX,[VAL1]
	PUSH	EBX
	CALL	SRCHL
	JNB	GET45

		MOV	DWORD [VAL1],7FFFFFH

	MOV	AL,[PASS]
	CMP	AL,1
;	JE	GET45
	;JNE	ERR9		;if label not found & PASS!=1

	MOV	AL,ID_EXTRN
	MOV	[ATTR],AL

	PUSHAD
	XOR	EAX,EAX
	MOV	AL,[WORK2]
	ADD	EAX,WORK2+1
	MOV	DWORD [EAX],12345678H
	POPAD

	PUSH	DWORD [LBL.NX]

	PUSHAD
	MOV	DWORD [SET.LBL.PNT],WORK2
	MOV	EDX,[LBL.NX]
	CALL	SET.LBL
	MOV	[LBL.NX],EDX
	POPAD

	POP	DWORD [PNT2]

	INC	DWORD [LBL.NO]

	MOV	EBX,[LBL.NX]
	CMP	EBX,EOM
	JNB	ERR8

	POP	EBX
	XOR	EBX,EBX
	PUSH	EBX	;VAL1

GET45:
		MOV	EDX,[PNT2]
		MOV	[NAME_PNT1],EDX
	MOV	EDX,[VAL1]
		MOV	AL,[ATTR]

	POP	EBX
	MOV	[VAL1],EBX
	;POP	EAX
	MOV	[ATTR1],AL
	XCHG	EDX,EBX
	JMP	GETN50

GETN55:

GETN50:
	OR	AL,AL
	RET

GETN60:	MOV	CL,AL
	INC	ESI
	MOV	EBX,0
	MOV	AL,[ESI]
	CMP	AL,CL
	JE	ERR7
;
;
	CMP	AL,7FH
	JE	ERR7
	MOV	BH,AL
	INC	ESI
	MOV	AL,[ESI]
	CMP	AL,CL
	JE	GETN66
	MOV	BL,AL
	INC	ESI
	MOV	AL,[ESI]
	CMP	AL,CL
	JNE	ERR6
GETN65:	INC	ESI
	JMP	GETN50

GETN66:	MOV	BL,BH
	;MOV	BH,0
	MOVZX	EBX,BL
	JMP	GETN65

GETN70:	INC	ESI
;	XOR	EBX,EBX
	MOV	EBX,[O.ADR1]
	JMP	GETN50


;---------------------

;
;	DX:label set adrs
;	WORK:data
;
;	>DX:label next pnt (when PASS 1)
;
;	PASS2 ‚Å‚Í‚¶‚ß‚ÄSET‚³‚ê‚éŽ–‚ª‚ ‚é->LINK is unfix->error ?

SET.LBL:
		CMP	EDX,W.LBL
		JB	ERR0

	MOV	[LBL.PNT.NEXT],EDX

;	CMP	BYTE [PASS],1
;	JNE	SET.LBL50	;if PASS!=1

	MOV	EBX,[SET.LBL.PNT]	;WORK1
	CALL	HASH

	MOVZX	ECX,AL
	SHL	ECX,2		;CX=AL*2
	MOV	EBX,[ECX+LBL.PNT.TOP]	;if [LBL.PNT.TOP+CX]!=0
	OR	EBX,EBX
	JNE	SET.LBL10

	MOV	[ECX+LBL.PNT.TOP],EDX
	JMP	SET.LBL20

SET.LBL10:
	MOV	EDX,[ECX+LBL.PNT]
	INC	EDX		;DX=[LBL.PNT+CX]+1

	MOV	EBX,[LBL.PNT.NEXT]
	MOV	[EDX],EBX	;link		;[DX]=[LBL.PNT.NEXT]

SET.LBL20:
	MOV	EDX,[LBL.PNT.NEXT]
	MOV	[ECX+LBL.PNT],EDX

SET.LBL50:
	MOV	EBX,[SET.LBL.PNT]	;WORK1

	MOV	AL,[EBX]	;len
	MOV	[EDX],AL
	INC	EBX
	INC	EDX

		JMP	SET.LBL60

	CMP	BYTE [PASS],1
	JE	SET.LBL60	;if PASS==1

	ADD	EDX,4		;for link
	JMP	SET.LBL70
SET.LBL60:
	MOV	DWORD [EDX],0
	ADD	EDX,4

SET.LBL70:
	;MOV	AL,[WORK1]
	MOV	EAX,[SET.LBL.PNT]
	MOV	AL,[EAX]

	MOVZX	ECX,AL
	ADD	ECX,4		;CX=len+4;for value
;
;LDIR:
	PUSH	ESI
	PUSH	EDI

	MOV	ESI,EBX
	MOV	EDI,EDX
	CLD
	REP
	MOVSB

	MOV	AL,[ATTR]
	STOSB

	MOV	EBX,ESI
	MOV	EDX,EDI

	POP	EDI
	POP	ESI

	RET


;
;	[PNT2]:data adrs
;
;	>CF:0(found)/1:(not found)
;	>[VAL1]:value
;	>[PNT2]:top pnt
;
;	?AL
;
;	+0 len
;	+1 next
;	+5 name,...0
;	   value
;

SRCHL:
	PUSH	ESI
	PUSH	EDI
	PUSH	ECX

	PUSH	EBX
	MOV	EBX,[PNT2]
	CALL	HASH
	POP	EBX

SRCHL.05:
	MOVZX	ECX,AL
	SHL	ECX,2
	MOV	ESI,[ECX+LBL.PNT.TOP]		;BX=[LBL.PNT.TOP+AL*2]
	OR	ESI,ESI
	JE	SRCHL4

SRCHL1:
	MOV	EDI,[PNT2]

	MOV	AL,[EDI]
	SUB	AL,[ESI]

	PUSH	ESI		;save top adrs
	INC	EDI
	INC	ESI

	MOV	ECX,[ESI]
	ADD	ESI,4

	MOV	[LBL.PNT.NEXT],ECX
	OR	AL,AL
	JNE	SRCHL3		;if len is different

	MOVZX	ECX,BYTE [EDI-1]

SRCHL2:	REP
	CMPSB
	JNE	SRCHL3

	;MOV	AL,[EBX]
	;CMP	AL,[EDX]
	;JNE	SRCHL3	;if different letter
	;INC	EBX
	;INC	EDX
	;DEC	CH
	;JNE	SRCHL2

	MOV	EDI,[ESI]

		MOV	AL,[ESI+4]
		MOV	[ATTR],AL

	MOV	[VAL1],EDI	;[VAL1]=[BX] value
	POP	ESI
	MOV	[PNT2],ESI	;[PNT2]=BX top pnt

	POP	ECX
	POP	EDI
	POP	ESI
	OR	AL,AL
	RET
SRCHL3:
	POP	ESI

	MOV	ESI,[LBL.PNT.NEXT]
	OR	ESI,ESI
	JE	SRCHL4
	JMP	SRCHL1
SRCHL4:
	POP	ECX
	POP	EDI
	POP	ESI
	STC
	RET



;
;
;
LOWER:	CMP	AL,'A'
	JB	_RET
	CMP	AL,'Z'+1
	JNB	_RET
	ADD	AL,20H
	RET

ERR_MEM:
	MOV	EDX,EMES_MEM
	JMP	ERR_STOP
;
;
;
ERR_STOP:
	MOV	BYTE [EXITCODE],2

	CALL	ERRSUB
	MOV	AL,[FLAG_FILE2]
	OR	AL,AL
	JE	ERR_STOP.10

	MOV	EBX,[HDL2]
	MOV	AX,3E00H
	CALL	INT21_close	;INT	21H

ERR_STOP.10:
	MOV	AL,1
	MOV	AH,4CH
	CALL	INT21_exit	;INT	21H

ERR_CONT:
	MOV	BYTE [EXITCODE],1

	;MOV	[PNT],ESI

	CALL	ERRSUB

;	MOV	ESI,[PNT]
;	MOV	AL,[ESI]
;	CALL	DOS02
;	MOV	AL,[ESI+1]
;	CALL	DOS02

	MOV	ESP,[STACK]
	CMP	BYTE [FLG.LINK],0
	JNE	ERR_CONT#
	JMP	MAIN50
ERR_CONT#:
	JMP	ERR_STOP.10

;
STRCHR:
	MOV	AH,[ESI]
	CMP	AH,AL
	JE	STRCHR.R
	OR	AH,AH
	JE	STRCHR.N
	INC	ESI
	JMP	STRCHR

STRCHR.N:
	XOR	EAX,EAX
	INC	EAX
STRCHR.R:
	RET


;

INIT_MEMORY:
	MOV	EAX,EXTRN_LINK_MEMORY_SIZE
	PUSH	EAX

	CALL	ALLOC
	MOV	[EXTRN_LINK_MEMORY],EAX
	MOV	[EXTRN_LINK_PNT],EAX

	POP	ECX
	ADD	EAX,ECX
	MOV	[EXTRN_LINK_END],EAX


	RET

;

UNINIT_MEMORY:
	MOV	EAX,[EXTRN_LINK_MEMORY]
	CALL	FREE
	XOR	EAX,EAX
	MOV	[EXTRN_LINK_MEMORY],EAX
	RET


#INCLUDE <ASM-COM.ASM>

_TEXT_FILL:
	;ds	HEADER+0600h-$
	ALIGN	FILE_ALIGN	;200h
_TEXT_END:


#INCLUDE <IMPORT-C.ASM>


;-----------------------

_DATA:

#INCLUDE <ASM-COMD.ASM>

;
CLINE:	DEFS	500H




EMES_MEM:
	DB	'memory full$'

MES1:	DB	'==== 8086 Self Assembler ===='
	DB	0DH,0AH
	DB	'       Programed by djnz80a'
	DB	0DH,0AH,'$'

EXITCODE:	DD	0
FILECL:	DEFS	200


FLG.LINK:DD	0

FLG.S:	DB	0
FLG.CL:	DB	0
FLG.J:	DB	0
FLG.A:	DB	0
FLG.Q:	DB	0

;
LOC:	DD	0

TOP_LOC:	DD	0
FNAMES_PNT:	DD	0
STR_REL:DB	'.REL',0

NOT_FIRST_MODULE:	DD	0

LINK_ID:	DD	0

PUTCHR_ERR_BUF:	DEFS	10H

PATCH_VAL:	DD	0

SYMS_ATTR:	DD	0
SYMS_VAL:	DD	0

IS_EQU:	DD	0

C.ATTR_AL:	DD	0

PRE_SEG:	DB	0

SETABS_BUF_PNT:	DD	SETABS_BUF

SETABS_BUF:	DEFS	20H

SET.LBL.PNT:	DD	0

EXTRN_LINK_PNT:	DD	0	;EXTRN_LINK
EXTRN_LINK_END:	DD	0

GETEXTRN.FLG:	DD	0


ATTR1:	DD	0
VAL1:	DD	0
NAME_PNT1:	DD	0

ATTR2:	DD	0
VAL2:	DD	0
NAME_PNT2:	DD	0


NAME_PNT:	DD	0

ATTR:	DD	0





FNAMES:	DEFS	4*100H

EXTRN_LINK_MEMORY:	DD	0

USRDMA:

	;END



_DATA_FILL:
	;ds	HEADER+0a00h-$
	ALIGN	FILE_ALIGN	;200h

_DATA_END:

#INCLUDE <DEBUG-C.ASM>

